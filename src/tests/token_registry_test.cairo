use core::option::OptionTrait;
use snforge_std::{ContractClassTrait, DeclareResultTrait, declare};
use starknet::{ContractAddress, get_contract_address};
use crate::interfaces::erc20::IERC20Dispatcher;
use crate::lens::token_registry::ITokenRegistryDispatcherTrait;
use crate::lens::token_registry::TokenRegistry::{
    FeltIntoByteArray, Registration, get_string_metadata, ten_pow,
};
use crate::tests::helper::{Deployer, DeployerTrait, EventLoggerTrait, event_logger};
use crate::tests::mock_erc20::MockERC20IERC20ImplTrait;

#[test]
fn test_ten_pow() {
    assert_eq!(ten_pow(0), 1);
    assert_eq!(ten_pow(1), 10);
    assert_eq!(ten_pow(2), 100);
    assert_eq!(ten_pow(3), 1_000);
    assert_eq!(ten_pow(4), 10_000);
    assert_eq!(ten_pow(5), 100_000);
    assert_eq!(ten_pow(6), 1_000_000);
    assert_eq!(ten_pow(18), 1_000_000_000_000_000_000);
}

#[starknet::interface]
trait ITestTarget<T> {
    fn a(self: @T) -> felt252;
    fn b(self: @T) -> ByteArray;
}

#[starknet::contract]
mod TestTarget {
    use starknet::storage::{StoragePointerReadAccess, StoragePointerWriteAccess};

    #[storage]
    struct Storage {
        a: felt252,
        b: ByteArray,
    }

    #[constructor]
    fn constructor(ref self: ContractState, a: felt252, b: ByteArray) {
        self.a.write(a);
        self.b.write(b);
    }

    #[abi(embed_v0)]
    impl TestTargetImpl of super::ITestTarget<ContractState> {
        fn a(self: @ContractState) -> felt252 {
            self.a.read()
        }

        fn b(self: @ContractState) -> ByteArray {
            self.b.read()
        }
    }
}


fn deploy_test_target(a: felt252, b: ByteArray) -> ContractAddress {
    let mut args: Array<felt252> = array![];
    Serde::serialize(@a, ref args);
    Serde::serialize(@b, ref args);

    let contract = declare("TestTarget").unwrap().contract_class();
    let (address, _) = contract.deploy(@args).expect('test target deploy');

    address
}


#[test]
fn test_felt252_into_byte_array() {
    assert_eq!(FeltIntoByteArray::into('abc'), "abc");
    assert_eq!(FeltIntoByteArray::into(''), "");
    assert_eq!(
        FeltIntoByteArray::into('1234567890123456789012345678901'),
        "1234567890123456789012345678901",
    );
}

#[test]
fn test_get_string_metadata() {
    let tt = deploy_test_target('abc', "abc");
    assert_eq!(get_string_metadata(tt, selector!("a")), get_string_metadata(tt, selector!("b")));
    // max length
    let tt = deploy_test_target(
        '1234567890123456789012345678901', "1234567890123456789012345678901",
    );
    assert_eq!(get_string_metadata(tt, selector!("a")), get_string_metadata(tt, selector!("b")));
    // longer than max length
    let tt = deploy_test_target('', "12345678901234567890123456789012345678901234567890");
    assert_eq!(
        get_string_metadata(tt, selector!("b")),
        "12345678901234567890123456789012345678901234567890",
    )
}

#[test]
fn test_register() {
    let mut d: Deployer = Default::default();
    let mut logger = event_logger();

    let core = d.deploy_core();
    let erc20 = d
        .deploy_mock_token_with_balance(get_contract_address(), 0xffffffffffffffffffffffffffffffff);
    let registry = d.deploy_token_registry(core);
    // 1e18
    erc20.transfer(registry.contract_address, 1_000_000_000_000_000_000);
    assert_eq!(erc20.balanceOf(registry.contract_address), 1_000_000_000_000_000_000_u256);
    registry.register_token(IERC20Dispatcher { contract_address: erc20.contract_address });
    let registration: Registration = OptionTrait::unwrap(logger.pop_log(registry.contract_address));
    assert_eq!(
        registration,
        Registration {
            address: erc20.contract_address,
            name: "",
            symbol: "",
            decimals: 18,
            total_supply: 0xffffffffffffffffffffffffffffffff,
        },
    )
}
